/ tpcd 17 queries
select sum q,sum x,sum xt,sum xd,n:count i by v,u from l
10#desc select s.x from ps where p.z=15,p.t like"*BRASS",s.n.r=`EUROPE,x=(min;x)fby p
10#desc select sum rev by o from l where o.c.m=`BUILDING,o.d<1995.03.15,ds>1995.03.15
select count i by j from distinct exec o from l where o.d within 1993.07.01 1993.09.30,dc<dr
desc select sum rev by s.n from l where o.d.year=1994,s.n.r=`ASIA,o.c.n=s.n
select x wsum xd from l where ds.year=1994,xd within .05 .07,q<24
select sum rev by s.n,n1:o.c.n,ds.year from l where ds.year in 1995 1996,((s.n=`FRANCE)&o.c.n=`GERMANY)|(s.n=`GERMANY)&o.c.n=`FRANCE
select rev wavg s.n=`BRAZIL by o.d.year from l where o.c.n.r=`AMERICA,o.d.year in 1995 1996,p.t like`$"ECONOMY ANODIZED STEEL"
select sum rev-q*ps.x by s.n,o.d.year from l where p.name like"*green*"
20#desc select sum rev by o.c from l where o.d within 1993.10.01 1993.12.31,v=`R
desc select from(select q wsum x by p from ps where s.n=`GERMANY)where x>sum x%count s
select count i by h,b:o.j>`$"2-HIGH" from l where dr.year=1994,h in`MAIL`SHIP,dc<dr,ds<dc
select sum rev by o.d.year from l where o.k=`$"Clerk#000000088",v=`R
select 100*rev wavg p.t like"PROMO*"from l where ds within 1995.09.01 1995.09.30
1#desc select sum rev by s from l where ds within 1996.01.01 1996.03.31
desc select count distinct s by p.b,p.t,p.z from ps where not p.b=`$"Brand#45",not p.t like"MEDIUM POLISHED*",p.z in 49 14 23 45 19 3 36 9,not s.cz like"*Better B*"
select sum x%7 from l where p.b=`$"Brand#23",p.e=`$"MED BOX",q<.2*(avg;q)fby p